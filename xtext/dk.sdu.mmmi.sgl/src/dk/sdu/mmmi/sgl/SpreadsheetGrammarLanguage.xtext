grammar dk.sdu.mmmi.sgl.SpreadsheetGrammarLanguage with org.eclipse.xtext.common.Terminals

generate spreadsheetGrammarLanguage "http://www.sdu.dk/mmmi/sgl/SpreadsheetGrammarLanguage"

Grammar:
	'language' name=ID ':' root=[Block] ';' elements+=Element*;
	
Element: Block | Rule;

Block:
	'block' name=ID '{' (columns+=Column)* '}'
;

Column:
	name=ID (multiple?='*')? def=ColumnDefinition ';'
;

ColumnDefinition:
	MandatoryColumn | OptionalColumn
;

MandatoryColumn:
	'=' spec=ColumnSpec
;

OptionalColumn:
	'?=' spec=ColumnSpec
;

ColumnSpec:
	RowSpec | BlockSpec
;

RowSpec:
	'column' header=STRING ':' syntax=Syntax
;

BlockSpec:
	'block' kind=[Block]
;

Syntax:
	is_id?='ID' | is_string?='STR' | is_int?='INT' | '@' token=STRING | 'rule' rule=[Rule]
;

Rule:
	'rule' name=ID ':' alternatives+=SyntaxSeq ('|' alternatives+=SyntaxSeq)* ';'
;

SyntaxSeq:
	(parts+=Syntax)+
;